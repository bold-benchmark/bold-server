PREFIX : <http://ti.rw.fau.de/sim#>
PREFIX time: <http://www.w3.org/2006/time#>

DELETE {
    GRAPH <sim> {
        ?sim :currentIteration ?oldIt ;
             :currentTime ?oldInstant .
        ?oldInstant time:inXSDDateTimeStamp ?oldTime ;
                    time:inDateTime ?oldDesc .
        ?oldDesc time:year ?oldYear ;
                 time:month ?oldMonth ;
                 time:day ?oldDay ;
                 time:hour ?oldHour ;
                 time:minute ?oldMinute ;
                 time:second ?oldSecond .
    }
} INSERT {
    GRAPH <sim> {
        ?sim :currentIteration ?it ;
             :currentTime [
                 time:inXSDDateTimeStamp ?time ;
                 time:inDateTime [
                     time:year ?year ;
                     time:month ?month ;
                     time:day ?day ;
                     time:hour ?hour ;
                     time:minute ?minute ;
                     time:second ?second
                 ]
             ].
    }
} WHERE {
    ?sim :currentIteration ?oldIt .
    BIND (?oldIt + 1 AS ?it)

    OPTIONAL {
        ?sim :currentTime ?oldInstant ;
             :timeslotDuration ?duration .

        ?oldInstant time:inXSDDateTimeStamp ?oldTime ;
                    time:inDateTime ?oldDesc .
        ?oldDesc time:year ?oldYear ;
                 time:month ?oldMonth ;
                 time:day ?oldDay ;
                 time:hour ?oldHour ;
                 time:minute ?oldMinute ;
                 time:second ?oldSecond .

        BIND (:after(?oldTime, ?duration) AS ?time)
        BIND (year(?time) AS ?year)
        BIND (month(?time) AS ?month)
        BIND (day(?time) AS ?day)
        BIND (hours(?time) AS ?hour)
        BIND (minutes(?time) AS ?minute)
        BIND (seconds(?time) AS ?second)
    }
}