PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX brick: <http://buildsys.org/ontologies/Brick#>
PREFIX sosa: <http://www.w3.org/ns/sosa/>
PREFIX sim: <http://ti.rw.fau.de/sim#>

INSERT {
    ?occupant a sim:Occupant ;
              sim:workplace ?room .
} WHERE {
    ?room a brick:Room .
    BIND (iri(concat(str(?room), "#occupant")) AS ?occupant)
};

DELETE {
    GRAPH ?g { ?occupancy rdf:value ?oldState }
} INSERT {
    GRAPH ?g { ?occupancy rdf:value "off" }
} WHERE {
    ?sensor a brick:Occupancy_Sensor ;
            sosa:observes ?occupancy .
    ?occupancy rdf:value ?oldState ;
              foaf:isPrimaryTopicOf ?g .
}