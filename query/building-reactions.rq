PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX ssn: <http://www.w3.org/ns/ssn/>
PREFIX sosa: <http://www.w3.org/ns/sosa/>
PREFIX brick: <http://buildsys.org/ontologies/Brick#>
PREFIX bf: <http://buildsys.org/ontologies/BrickFrame#>
PREFIX sim: <http://ti.rw.fau.de/sim#>

# occupancy sensor gets updated if move action

DELETE {
    GRAPH ?fromGraph { ?fromOccupancy rdf:value ?fromState }
    GRAPH ?toGraph { ?toOccupancy rdf:value ?toState }
} INSERT {
    GRAPH ?fromGraph { ?fromOccupancy rdf:value "off" }
    GRAPH ?toGraph { ?toOccupancy rdf:value "on" }
} WHERE {
    <sim> sim:currentIteration ?time .

    # TODO delete action when done?
    ?action a sim:MoveAction ;
            sim:agent ?occupant ;
            sim:fromLocation ?from ;
            sim:toLocation ?to ;
            sim:atTime ?time .

    OPTIONAL {
        ?from bf:hasPoint ?fromSensor .
        ?fromSensor a brick:Occupancy_Sensor ; sosa:observes ?fromOccupancy .
        ?fromOccupancy rdf:value ?fromState ;
                       foaf:isPrimaryTopicOf ?fromGraph .
    }

    OPTIONAL {
        ?to bf:hasPoint ?toSensor .
        ?toSensor a brick:Occupancy_Sensor ; sosa:observes ?toOccupancy .
        ?toOccupancy rdf:value ?toState ;
                     foaf:isPrimaryTopicOf ?toGraph .
    }
};

# switch status gets updated (light, appliance)

DELETE {
    GRAPH ?g { ?lighting rdf:value ?oldState }
} INSERT {
    GRAPH ?g { ?lighting rdf:value ?state }
} WHERE {
    <sim> sim:currentIteration ?time .

    # TODO delete action when done?
    ?action a sim:SwitchAction ;
            sim:agent ?occupant ;
            sim:object ?switch ;
            sim:location ?loc ;
            sim:atTime ?time ;
            sim:result ?state .

    ?switch ssn:hasProperty ?lighting .
    ?lighting rdf:value ?oldState ;
              foaf:isPrimaryTopicOf ?g .
}

# illuminance gets updated (by lighting systems and sunlight)

# electric consumption gets updated

# thermostat gets updated (stateful update)

# temperature gets updated through the action of radiators