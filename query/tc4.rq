PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX sim: <http://ti.rw.fau.de/sim#>
PREFIX brick: <http://buildsys.org/ontologies/Brick#>
PREFIX bf: <http://buildsys.org/ontologies/BrickFrame#>
PREFIX ssn: <http://www.w3.org/ns/ssn/>
PREFIX sosa: <http://www.w3.org/ns/sosa/>
PREFIX qudt: <http://qudt.org/schema/qudt#>

SELECT ?it ?count WHERE {
    <sim> sim:currentIteration ?it .
    {
        SELECT (count(*) AS ?count) WHERE {
            ?sensor a brick:Luminance_Sensor ;
                    bf:isLocatedIn ?room ;
                    sosa:observes ?illuminance .
            ?illuminance qudt:numericValue ?val .

            FILTER (?val < 500) # 500 lux: office lighting

            ?switch a brick:Luminance_Command ;
                    bf:isLocatedIn ?room ;
                    sosa:actsOnProperty ?lighting .
            ?lighting rdf:value ?state .

            ?room a brick:Room .

             FILTER (?state != "off")
        }
    }
}