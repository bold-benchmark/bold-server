PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX sim: <http://ti.rw.fau.de/sim#>
PREFIX brick: <http://buildsys.org/ontologies/Brick#>
PREFIX bf: <http://buildsys.org/ontologies/BrickFrame#>
PREFIX ssn: <http://www.w3.org/ns/ssn/>
PREFIX sosa: <http://www.w3.org/ns/sosa/>
PREFIX qudt: <http://qudt.org/schema/qudt#>

SELECT ?lighting WHERE {
	?sensor1 a brick:Occupancy_Sensor ;
			 bf:isLocatedIn ?room ;
			 sosa:observes ?occupancy .
	?occupancy rdf:value ?occupancyValue .

	?sensor2 a brick:Luminance_Sensor ;
			 bf:isLocatedIn ?room ;
			 sosa:observes ?illuminance .
	?illuminance qudt:numericValue ?illuminanceValue .

	?switch a brick:Luminance_Command ;
			bf:isLocatedIn ?room ;
			sosa:actsOnProperty ?lighting .
	?lighting rdf:value ?lightingValue .

	?sp a brick:Luminance_Setpoint ;
		  bf:isLocatedIn ?room ;
		  ssn:hasProperty ?desiredIlluminance .
	?desiredIlluminance qudt:numericValue ?desiredIlluminanceValue .

	?room a brick:Room .

	FILTER (if(?occupancyValue = "off" || ?illuminanceValue > ?desiredIlluminanceValue, ?lightingValue != "off", ?lightingValue != "on"))
}