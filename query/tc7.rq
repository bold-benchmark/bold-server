PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX sim: <http://ti.rw.fau.de/sim#>
PREFIX brick: <http://buildsys.org/ontologies/Brick#>
PREFIX bf: <http://buildsys.org/ontologies/BrickFrame#>
PREFIX ssn: <http://www.w3.org/ns/ssn/>
PREFIX sosa: <http://www.w3.org/ns/sosa/>
PREFIX qudt: <http://qudt.org/schema/qudt#>

SELECT ?it ?count ?countRef WHERE {
    <sim> sim:currentIteration ?it .
    {
        SELECT (count(*) as ?count) WHERE {
        	?room a brick:Room .

            ?sensor1 a brick:Occupancy_Sensor ;
                     bf:isLocatedIn ?room ;
                     sosa:observes ?occupancy .
            ?occupancy rdf:value ?occupancyValue .

            ?sensor2 a brick:Luminance_Sensor ;
                     bf:isLocatedIn ?room ;
                     sosa:observes ?lighting .
            ?lighting qudt:numericValue ?lightingValue .

            ?sp a brick:Luminance_Setpoint ;
                  bf:isLocatedIn ?room ;
                  ssn:hasProperty ?desiredLighting .
            ?desiredLighting qudt:numericValue ?desiredLightingValue .

			FILTER (?occupancyValue = "on" && ?lightingValue > 500.0 && ?lightingValue < ?desiredLightingValue)
        }
    }
    {
        SELECT (count(*) as ?countRef) WHERE {
        	?room a brick:Room .

            ?sensor1 a brick:Occupancy_Sensor ;
                     bf:isLocatedIn ?room ;
                     sosa:observes ?occupancy .
            ?occupancy rdf:value ?occupancyValue .

            ?sensor2 a brick:Luminance_Sensor ;
                     bf:isLocatedIn ?room ;
                     sosa:observes ?lighting .
            ?lighting qudt:numericValue ?lightingValue .

			FILTER (?occupancyValue = "on" && ?lightingValue > 500.0)
        }
    }
}