PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX ssn: <http://www.w3.org/ns/ssn/>
PREFIX sosa: <http://www.w3.org/ns/sosa/>
PREFIX qudt: <http://qudt.org/schema/qudt#>
PREFIX time: <http://www.w3.org/2006/time#>
PREFIX brick: <http://buildsys.org/ontologies/Brick#>
PREFIX bf: <http://buildsys.org/ontologies/BrickFrame#>
PREFIX sim: <http://ti.rw.fau.de/sim#>

INSERT {
    GRAPH <sim> {
        <sim> sim:sunriseTime [ time:inXSDDateTimeStamp ?sunrise ] ;
              sim:sunsetTime [ time:inXSDDateTimeStamp ?sunset ] .
    }
} WHERE {
    <sim> sim:currentTime ?userTime .
    ?userTime time:inXSDDateTimeStamp ?time .

    BIND (str(year(?time)) AS ?year)
    BIND (concat(if(month(?time) < 10, "0", ""), str(month(?time))) AS ?month)
    BIND (concat(if(day(?time) < 10, "0", ""), str(day(?time))) AS ?day)
    BIND (concat(?year, "-", ?month, "-", ?day) AS ?date)
    BIND (tz(?time) AS ?tz)

    # TODO compute them according to the starting date instead
    BIND ("06:00:00" AS ?sunriseTime)
    BIND ("21:00:00" AS ?sunsetTime)

    BIND (xsd:dateTime(concat(?date, "T", ?sunriseTime, ?tz)) AS ?sunrise)
    BIND (xsd:dateTime(concat(?date, "T", ?sunsetTime, ?tz)) AS ?sunset)
};

INSERT {
	[] a sim:CloudCoverageStimulus ;
	   sim:startCoverage ?start ;
	   sim:endCoverage ?end .
} WHERE {
	<sim> sim:sunriseTime ?sunriseTimestamp ;
		  sim:sunsetTime ?sunsetTimestamp .

	?sunriseTimestamp time:inXSDDateTimeStamp ?sunrise .
	?sunsetTimestamp time:inXSDDateTimeStamp ?sunset .

	# reference coverage values in [0,1] at sunrise and sunset

	BIND (sim:rand(?sunrise) AS ?start)
	BIND (sim:rand(?sunset) AS ?end)
};

INSERT {
	?room sim:orientation ?orientation ;
	      sim:occlusionFactor ?occlusion .
} WHERE {
	?room a brick:Room .
	# coin flipping to decide whether a room is oriented towards east or west
	BIND (if(sim:rand(?room) < 0.5, sim:west, sim:east) AS ?orientation)
	# occlusion factor in [0.5, 1]
	BIND (0.5 + (sim:rand(?room) / 2) AS ?occlusion)
};

DELETE {
    GRAPH ?g { ?lighting rdf:value ?oldState }
} INSERT {
    GRAPH ?g { ?lighting rdf:value "off" }
} WHERE {
    ?sensor a brick:Occupancy_Sensor ;
            sosa:observes ?occupancy .
    ?occupancy rdf:value ?oldState ;
              foaf:isPrimaryTopicOf ?g .
};

DELETE {
    GRAPH ?g { ?lighting rdf:value ?oldState }
} INSERT {
    GRAPH ?g { ?lighting rdf:value "off" }
} WHERE {
    ?switch a brick:Luminance_Command ;
            sosa:actsOnProperty ?lighting .
    ?lighting rdf:value ?oldState ;
              foaf:isPrimaryTopicOf ?g .
};

DELETE {
    GRAPH ?g { ?illuminance qudt:numericValue ?oldVal }
} INSERT {
    GRAPH ?g { ?illuminance qudt:numericValue 0.0 }
} WHERE {
    ?sensor a brick:Luminance_Sensor ;
            bf:isLocatedIn ?room ;
            sosa:observes ?illuminance .
    ?illuminance foaf:isPrimaryTopicOf ?g .
    ?room a brick:Room .
    OPTIONAL { ?illuminance qudt:numericValue ?oldVal }
};

INSERT {
    GRAPH ?st {
    	?sp a brick:Luminance_Setpoint ;
    	    bf:isLocatedIn ?room ;
    	    ssn:hasProperty ?property .
    }
    GRAPH ?g {
    	?property a ssn:Property ;
    	          foaf:isPrimaryTopicOf ?g ;
    	          qudt:numericValue 500.0 .
    }
} WHERE {
    ?switch a brick:Luminance_Command ;
            bf:isLocatedIn ?room ;
            sosa:actsOnProperty ?lighting .
	?room a brick:Room .

	BIND (iri(concat(str(?room), "_LUX_SPT")) AS ?sp)
	BIND (iri(concat(str(?room), "_LUX_SPT-property")) AS ?g)
	BIND (iri(concat(str(?room), "_LUX_SPT-property#it")) AS ?property)
};

INSERT {
    ?occupant a sim:Occupant ;
              sim:workplace ?room .
} WHERE {
    ?room a brick:Room .
    BIND (iri(concat(str(?room), "-occupant")) AS ?occupant)
}